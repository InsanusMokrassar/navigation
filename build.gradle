buildscript {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath libs.android.tools.build
        classpath libs.android.dexcount
        classpath libs.kotlin.gradle.plugin
        classpath libs.kotlin.serialization.plugin
        classpath libs.kotlin.dokka.plugin
        classpath libs.github.release
    }
}

plugins {
    alias(libs.plugins.kotlin.dokka)
    alias(libs.plugins.versions)
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://nexus.inmo.dev/repository/maven-releases/" }
    }
}

apply from: "./extensions.gradle"
apply from: "./github_release.gradle"
